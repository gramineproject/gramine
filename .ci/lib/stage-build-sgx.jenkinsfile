stage('build') {
    sh '''
        git clone https://github.com/intel/linux-sgx-driver.git
        cd linux-sgx-driver
        git checkout 276c5c6a064d22358542f5e0aa96b1c0ace5d695
    '''

    env.MESON_OPTIONS = ''
    if (env.UBSAN == '1') {
        env.MESON_OPTIONS += ' -Dubsan=enabled'
    }
    if (env.ASAN == '1') {
        env.MESON_OPTIONS += ' -Dasan=enabled'
    }
    if (env.CC == 'clang') {
        env.MESON_OPTIONS += ' -Dmusl=disabled'
    }
    if (env.RA_TYPE == 'dcap') {
        env.MESON_OPTIONS += ' -Ddcap=enabled'
    }

    if (env.SGX_DRIVER == null) {
        env.SGX_DRIVER = 'oot'
    }
    if (env.SGX_DRIVER == 'oot') {
        env.MESON_OPTIONS += ' -Dsgx_driver_include_path=' + env.WORKSPACE + '/linux-sgx-driver'
    }

    try {
        sh '''
            meson setup build/ \
                --werror \
                --prefix="$PREFIX" \
                --buildtype="$BUILDTYPE" \
                -Ddirect=disabled \
                -Dsgx=enabled \
                -Dtests=enabled \
                -Dsgx_driver="$SGX_DRIVER" \
                $MESON_OPTIONS
            ninja -vC build/
        '''

        // install
        sh '''
            ninja -vC build/ install
            gramine-sgx-gen-private-key
        '''
    } finally {
        archiveArtifacts 'build/meson-logs/**/*'
        archiveArtifacts 'build/subprojects/glibc-*/glibc-build.log'
    }

    // archive all installed files
    // NOTE we can't use ${env.PREFIX} here, because path needs to be relative to workdir
    archiveArtifacts "usr/**/*"

    // Absolute path to libdir, as configured by Meson.
    // For our current builds this should be "$WORKSPACE/usr/lib/x86_64-linux-gnu":
    // --prefix is set from $PREFIX above (see config-docker.jenkinsfile) and should be "$WORKSPACE/usr";
    // --libdir is distro-dependent, but on Debian and derivatives it's "lib/x86_64-linux-gnu"
    libdir = sh(returnStdout: true, script: '''
        meson introspect build/ --buildoptions \
        | jq -r '(map(select(.name == "prefix")) + map(select(.name == "libdir"))) | map(.value) | join("/")'
    ''').trim()

    env.GRAMINE_PKGLIBDIR = libdir + '/gramine'

    // In CI we install to non-standard --prefix (see above). This makes sure the libraries are
    // available anyway.
    env.PKG_CONFIG_PATH = libdir + '/pkgconfig'

    // prevent cheating and testing from repo
    sh 'rm -rf build'
    sh 'git clean -Xf subprojects'
}
